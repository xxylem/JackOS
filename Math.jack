// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

    static int wordLength, wMinus1;
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {

        let wordLength = 16;
        let wMinus1 = 15;

        let twoToThe = Array.new(15);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = -32768;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {

        if ( x < 0 ) {
            return ( -x );
        }

        else {
            return x;
        }
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {

        var int sum, i, shiftedX;

        let sum = 0;
        let i   = 0;
        let shiftedX = x;

        while ( i < wMinus1 ) {
            if ( Math.bit( y, i ) ) {
                let sum = sum + shiftedX;
            }

            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;
    }

    function boolean bit(int x, int i) {
        
        var int compareVal;

        let compareVal = twoToThe[15 - i];

        return ( (x & compareVal) = compareVal );
        
        
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        
        var boolean positive;
        var int result;

        if ((( x > 0 ) & ( y > 0 )) | (( x < 0 ) & ( y < 0))) {
            let positive = true;
        }
        else {
            let positive = false;
        }

        let result = Math.divideHelper( Math.abs(x), Math.abs(y) );

        if (positive) {
            return result;
        }
        else {
            return (- result);
        }
    }

    function int divideHelper(int x, int y) {

        var int q;

        if (( y > x ) | ( y < 0 )) {
            return 0;
        }

        let q = Math.divideHelper( x, y + y );

        if (( x - ( ( q + q ) * y )) < y ) {
            return ( q + q );
        }
        else {
            return ( q + q + 1);
        }
    }



    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {

        var int y, j, cond, condSq;

        let y = 0;
        let j = 8;

        while ( j > (-1) ) {

            let cond = ( y + twoToThe[j] );
            let condSq = cond * cond;

            if (( condSq < ( x + 1)) & ( condSq > 0 )) {
                let y = cond;
            }

            let j = j - 1;
        }

        return y;



    }

    /** Returns the greater number. */
    function int max(int a, int b) {

        if ( b > a ) {
            return b;
        }
        else {
            return a;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {

        if (b < a) {
            return b;
        }
        else {
            return a;
        }
    }
}
